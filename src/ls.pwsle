// EarthOS-utils: ls
//
//    Copyright (C) 2021  adazem009
//
//    A minimal ls command to show files in the current working directory.
//    Also supports custom paths:
//    ls [path] [another path] ...
//
//    This program is free software: you can redistribute it and/or modify
//    it under the terms of the GNU General Public License as published by
//    the Free Software Foundation, either version 3 of the License, or
//    (at your option) any later version.
//
//    This program is distributed in the hope that it will be useful,
//    but WITHOUT ANY WARRANTY; without even the implied warranty of
//    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//    GNU General Public License for more details.
//
//    You should have received a copy of the GNU General Public License
//    along with this program.  If not, see <https://www.gnu.org/licenses/>.
//
// TODO: Add -l option
// Get args
getitem/"arg_1",1/"argv"
bintolist/argv/"args"
getlistlength/"args"/"argc"
set/"showall",0
setlist/"paths"
set/"i",0
while/[i<argc]
	calc/"i=i+1"
	getitem/"args",i/"path"
	getletter/path,1/"let"
	if/[let == '-']
		getlength/path/"len"
		set/"i2",1
		while/[i2 < len]
			calc/"i2=i2+1"
			getletter/path,i2/"let"
			if/[let == 'a']
				set/"showall",1
			else
				print/"ls: unknown option: '-",let,"'",\n
			endif
		endloop
	else
		append/path/"paths"
	endif
endloop
getlistlength/"paths"/"len2"
if/[len2 == 0]
	// Add current dir
	append/""/"paths"
	add/len2,1/"len2"
endif
set/"i3",0
repeat/len2
	calc/"i3=i3+1"
	getitem/"paths",i3/"path"
	if/[len2 > 1]
		print/path,": ",\n
	endif
	// Read directory
	ls/path/"list"
	set/"ret",%returnc
	if/[ret != ""]
		geterror/ret,0/"ret"
		print/"ls: ",path,": ",ret,\n
	else
		// Print file/dir names
		getlistlength/"list"/"len"
		set/"i2",0/"i",0
		while/[i < len]
			calc/"i=i+1"
			calc/"i2=i2+1"
			if/[i2 > 5]
				print/\n
				set/"i2",0
			endif
			getitem/"list",i/"it"
			getletter/it,1/"let"
			set/"show",1
			if/[let == '.']
				if/[showall == 0]
					set/"show",0
				endif
			endif
			if/[show == 1]
				print/it," "
			endif
		endloop
		if/[len > 0]
			print/\n
		endif
		if/[argc > 1]
			print/\n
		endif
	endif
endloop
