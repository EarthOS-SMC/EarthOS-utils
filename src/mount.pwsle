// EarthOS-utils: mount
//
//    Copyright (C) 2021  adazem009
//
//    Mounts a filesystem at specified mount point.
//
//    This program is free software: you can redistribute it and/or modify
//    it under the terms of the GNU General Public License as published by
//    the Free Software Foundation, either version 3 of the License, or
//    (at your option) any later version.
//
//    This program is distributed in the hope that it will be useful,
//    but WITHOUT ANY WARRANTY; without even the implied warranty of
//    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//    GNU General Public License for more details.
//
//    You should have received a copy of the GNU General Public License
//    along with this program.  If not, see <https://www.gnu.org/licenses/>.
//
getitem/"arg_1",1/"argv"
bintolist/argv/"args"
getlistlength/"args"/"argc"
if/[argc == 0]
	// TODO: Display all mounted filesystems
	print/"mount: coming soon",\n
	exit
endif
if/[argc == 1]
	getitem/"args",1/"pt"
	// TODO: Use a shared library to read fstab
	//
	// Read /etc/fstab
	getfile/"/etc/fstab"/"fstab"
	set/"ret",%returnc
	if/[ret != ""]
		print/"mount: /etc/fstab: ",ret,\n
		exit
	endif
	// Read every line
	getlistlength/"fstab"/"len"
	set/"i",0
	repeat/len
		calc/"i=i+1"
		getitem/"fstab",i/"line"
		getletter/line,1/"let"
		if/[let != "#"]
			setlist/"parts"
			getlength/line/"len2"
			set/"part",""/"i2",0
			repeat/len2
				calc/"i2=i2+1"
				getletter/line,i2/"let"
				set/"sp"," "
				if/[let == sp]
					append/part/"parts"
					set/"part",""
				else
					set/"part",part,let
				endif
			endloop
			append/part/"parts"
			// Device/filesystem
			getitem/"parts",1/"fs"
			// Mount point
			getitem/"parts",2/"pnt"
			// Mode
			getitem/"parts",3/"md"
			// Check input
			if/[pt == fs]
				set/"mnt",1
			endif
			if/[pt == pnt]
				set/"mnt",1
			endif
			if/[mnt == 1]
				mount/fs,pnt,md
				set/"ret",%returnc
				if/[ret != ""]
					geterror/ret,0/"ret"
					print/"mount: mount failed: ",ret,\n
				endif
				exit
			endif
		endif
	endloop
	print/"mount: ",pt,": can't find in /etc/fstab.",\n
	exit
else
	if/[argc != 2]
		print/"mount: bad usage",\n
		exit
	endif
	getitem/"args",2/"pnt"
endif
getitem/"args",1/"fs"
if/[%whoami != "root"]
	print/"mount: only root can do that",\n
	exit
endif
// File must be readable
ftest/fs,"r","f"/"test"
set/"ret",%returnc
if/[ret != ""]
	geterror/ret,0/"ret"
	print/"mount: ",fs,": ",ret,\n
	exit
endif
// File must be writable before it can be mounted rw
ftest/fs,"w","f"/"test"
set/"ret",%returnc
if/[ret != ""]
	geterror/ret,0/"ret"
	print/"mount: ",fs,": ",ret,\n
	print/"File not writable, trying read-only",\n
	set/"mode","ro"
else
	set/"mode","rw"
endif
// Mount point must be writable
ftest/pnt,"w","d"/"test"
set/"ret",%returnc
if/[ret != ""]
	print/"mount: ",pnt,": ",ret,\n
	exit
endif
mount/fs,pnt,mode
set/"ret",%returnc
if/[ret != ""]
	geterror/ret,0/"ret"
	print/"mount: mount failed: ",ret,\n
endif
